/*
 * Copyright to the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

project.version = '5.1.3'

task printVersion {
    description = 'Prints the current SORCER version.'
    doLast {
        logger.quiet "Version: $version"
    }
}


task printClasspath() {
    description = 'Prints the current Java classpath.'
    doLast {
        println "${project.name}"
        configurations.testRuntime.each {
            println "--> $it"
        }
    }
}

task wrapper(type: Wrapper) {
    description = 'Prints the Gradle wrapper version.'
    gradleVersion = '2.1'
}

ext {
    enclaveRepo = "10.131.7.138:7001"
    //enclaveRepo = "10.0.1.9:50001"
    enclave = InetAddress.getLocalHost().getHostAddress().startsWith("10.131")
}

apply from: file('gradle/libraries.gradle')
apply from: file("gradle/testProperties.gradle")
apply from: file("gradle/utils.gradle")

allprojects {
    group = 'org.sorcer'
}

subprojects {
    if (project.name != "examples" && project.name != "sorcer-int-tests") {
        apply plugin: "maven"
        apply plugin: "maven-publish"

        if (project.name != "distribution") {
            apply plugin: "java"
            apply from: file("${rootProject.projectDir.path}/gradle/bootme.gradle")

            publishing {
                publications {
                    mavenJava(MavenPublication) {
                        from components.java
                    }
                }
            }
        }
        publishing {
            repositories {
                maven {
                    url "http://$enclaveRepo"
                    //			url "http://repo1.maven.org/maven2"
                }
            }
        }
        repositories {
            mavenLocal()
            if (!enclave) {
                maven { url "http://www.rio-project.org/maven2" }
                maven { url "http://mvn.sorcersoft.com/content/groups/public" }
                mavenCentral()
            } else {
                maven { url "http://$enclaveRepo" }
            }
        }
    }

}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(":allTests")) {
        project.subprojects { project ->
            tasks.withType(Test) {
                /* Disable the test report for the individual test task */
                reports.html.enabled = false
            }
        }
    }
}

task allTests(type: TestReport, dependsOn: "bootSorcerRio") {
    description = 'Aggregates all tests, view them in build/reports/tests/index.html.'
    destinationDir = file("$buildDir/reports/allTests")
    project.subprojects { project ->
        tasks.withType(Test) {
            ignoreFailures true
            reportOn it
        }
    }
    doLast {
        println "\nThe allTests task is complete. See the results at: file://${destinationDir.path}/index.html"
    }

    finalizedBy("terminateSorcerRio")
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        //noinspection SpellCheckingInspection
        tasks.withType(Javadoc) {
            // disable the crazy super-strict doclint tool in Java 8
            //noinspection SpellCheckingInspection
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

/*
 * Generate all javadocs
 */
task allJavadoc(type: Javadoc) {
    options.links = ['http://docs.oracle.com/javase/8/docs/api/',
                     'https://river.apache.org/doc/api/',
                     'http://www.rio-project.org/apidocs']
    /*
    options.linksOffline 'http://docs.oracle.com/javase/8/docs/api/', 'docs/java'
    options.linksOffline 'https://river.apache.org/doc/api/', 'docs/river'
    options.linksOffline 'http://www.rioproject.org/apidocs','docs/rio'
     */
    exclude '**/*Test.java'
    exclude '**/edu/*'
    description = 'Creates SORCER API Specification, view them in build/javadoc/index.html'
    destinationDir = new File(project.projectDir, 'build/javadoc')
    title = "SORCER ${version}"
}

subprojects {
    afterEvaluate {
        def skip = ["deploy-tests", "sorcer-tester", "eol", "worker", "pml", "service", "ssb"]
        if (plugins.hasPlugin(JavaPlugin) && !skip.contains(project.name)) {
            // configuration here
            rootProject.tasks.allJavadoc {
                source += files(sourceSets.collect { srcSet -> srcSet.allJava })
                classpath += files(sourceSets*.compileClasspath)
            }

        }
    }
}


